plugins {
    id 'java'
}

repositories{
    mavenCentral()
}

dependencies {
    implementation "io.reactivex.rxjava3:rxjava:3.0.7"
    implementation "org.javatuples:javatuples:1.2"
    testCompile "junit:junit:4.11"
}

sourceSets {
    main {
        java {
            srcDir 'src/java'
            exclude "**/*Test.java"
        }
    }
    test {
        java {
            srcDir 'src/java'
        }
    }
}

// Generate JNI headers
task generateJniHeaders(type: JavaCompile) {
    classpath = sourceSets.main.compileClasspath
    destinationDir file("${buildDir}/useless/jni")
    source = sourceSets.main.java
    options.compilerArgs += [ '-h', file("src/jni")]
    doLast {
        delete file("${buildDir}/useless/jni")
    }
}

// Generate shared(.so) lib
task compileJni(type: Exec) {
    dependsOn { generateJniHeaders }
    commandLine 'make', 'compileJNI'
}

assemble {
    dependsOn compileJni
}